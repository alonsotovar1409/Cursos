*ANALISIS DE REGRESION CON UNA VARIABLE (REGRESIÓN LINEAL SIMPLE)
-  SE NECESITA UN R2 CON F SIGNIFICATIVO / menor a 0.05
-  LA REGRESION DEVUELVE UNA ECUACION: Lean betas
-  SOLO LOS COEFICIENTES SIGNIFICATIVOS SIRVEN PARA EXPLICAR EL MODELO.

REGRESSION
  /MISSING LISTWISE
  /STATISTICS COEFF OUTS R ANOVA
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=ENTER a_exp  .

*ANALISIS DE REGRESION CON DOS VARIABLES (REGRESIÓN LINEAL MÚLTIPLE)
-  SE NECESITA UN R2 CON F SIGNIFICATIVO
-  LA REGRESION DEVUELVE UNA ECUACION: 
   SOLO LOS COEFICIENTES SIGNIFICATIVOS "PASAN".

REGRESSION
  /MISSING LISTWISE
  /STATISTICS COEFF OUTS R ANOVA
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=ENTER   a_exp supf.

*FORWARD: 

REGRESSION
  /MISSING LISTWISE
  /STATISTICS COEFF OUTS R ANOVA
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=FORWARD a_exp supf  .

*BACKWARD

REGRESSION
  /MISSING LISTWISE
  /STATISTICS COEFF OUTS R ANOVA
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=BACKWARD a_exp supf  .

*GRÁFICO DE DISPERSIÓN DE PUNTOS.

GRAPH
  /SCATTERPLOT(XYZ)=supf WITH coste WITH a_exp
  /MISSING=LISTWISE .


IGRAPH /VIEWNAME='Diagrama de dispersión' /X1 = VAR(supf) TYPE = SCALE /Y =
  VAR(coste) TYPE = SCALE /X2 = VAR(a_exp) TYPE = SCALE /COORDINATE = THREE
 /FITLINE METHOD = REGRESSION LINEAR LINE = TOTAL SPIKE=OFF /X1LENGTH=9.0
 /YLENGTH=9.0 /X2LENGTH=9.0 /CHARTLOOK='C:\Archivos de'+
 ' programa\SPSS\Looks\Classic.clo' /SCATTER COINCIDENT = NONE.


* REQUISITOS:

*REQUISITO 1: NO DEBE HABER MULTICOLINEALIDAD: 
      UNA FORMA DE VER ESTO ES QUE LAS INDEPENDIENTES NO TENGAN ALTA CORRELACIÓN ENTRE ELLAS
      EL VIF NO DEBE PASAR DE 4 (LA TOLERANCIA debe ser mayor a  0.25)
      EL INDICE DE CONDICION NO DEBE SUPERAR A 20.

REGRESSION
  /MISSING LISTWISE
  /STATISTICS COEFF OUTS R ANOVA COLLIN TOL
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=FORWARD a_exp supf  .

* REQUISITO 2: VERIFICAR QUE EL MODELO NO PRESENTE RELACIÓN ESPÚREA
 ES DECIR, QUE UNA VARIABLE SEA SIGNIFICATIVA GRACIAS A OTRA
     NO DEBE DE HABER UNA GRAN DISMINUCÓN DESDE LA CORRELACION CERO
     PASANDO POR LA  CORRELACION PARCIAL A LA CORRELACION SEMIPARCIAL.
  
REGRESSION
  /MISSING LISTWISE
  /STATISTICS COEFF OUTS R ANOVA ZPP
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=ENTER a_exp supf  .


* REQUISITO 3: VERIFICAR QUE NO HAYA VALORES INFLUYENTES:
   LA DISTANCIA DE COOK NO DEBE PASAR DE 1 Y LA DEL LEVERAGE DE 0.2.

REGRESSION
  /MISSING LISTWISE
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=ENTER a_exp supf
  /SAVE COOK LEVER .

GRAPH
  /SCATTERPLOT(BIVAR)=LEV_1 WITH COO_1
  /MISSING=LISTWISE .

* REQUISITO 4: LOS RESIDUOS  DEBEN DISTRIBUIRSE NORMALMENTE.

REGRESSION
  /MISSING LISTWISE
  /STATISTICS COEFF OUTS R ANOVA
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=ENTER a_exp supf
  /RESIDUALS HIST(ZRESID) NORM(ZRESID)
  /SAVE RESID.

NPAR TESTS
  /K-S(NORMAL)= RES_1
  /MISSING ANALYSIS.

*REQUISITO 5: NO DEBE DE PRESENTARSE AUTOCORRELACIÓN. LOS RESIDUOS DEBEN SER INDEPENDIENTES ENTRE SÍ
    EL DURBIN WATSON DEBEN ESTAR ENTRE 1.5 Y 2.5.

REGRESSION
  /MISSING LISTWISE
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=ENTER a_exp supf
  /RESIDUALS DURBIN .

* REQUISITO 6: NO DEBE DE HABER HETEROCEDASTICIDAD (HETEROGENEIDAD DE VARIANZAS): 
  PRUEBA DE PARK: LOS RESIDUOS NO DEBEN MOSTRAR NINGUN PATRON.

REGRESSION
  /MISSING LISTWISE
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN
  /DEPENDENT coste
  /METHOD=ENTER a_exp supf
  /SCATTERPLOT=(*ZRESID ,*ZPRED ) 
/SAVE ZRESID .

* HAY QUE VER QUÉ VARIABLE GENERA LA HETEROCEDASTICIDAD 
   DISPERSIÓN DE PUNTOS ENTRE LAS VARIABLES PREDICTORAS CON EL RESIDUO ESTANDARIZADO.

GRAPH
  /SCATTERPLOT(BIVAR)=supf WITH ZRE_1
  /MISSING=LISTWISE .

GRAPH
  /SCATTERPLOT(BIVAR)=a_exp WITH ZRE_1
  /MISSING=LISTWISE .



